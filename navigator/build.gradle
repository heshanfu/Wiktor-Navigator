apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'  // crashlytics

android {
    compileSdkVersion 27
    buildToolsVersion '28.0-rc1'
    defaultConfig {
        applicationId 'pl.org.seva.navigator'
        minSdkVersion 21
        targetSdkVersion 27
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary true
        versionCode 44
        versionName '2.6.1'

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }

        lintOptions {
            checkReleaseBuilds false
        }
    }

    /* Android Extensions plugin includes several experimental
       features such as LayoutContainer support and a Parcelable
       implementation generator. These features are not considered
       production ready yet, so you need to turn on the experimental
       mode in build.gradle in order to use them:
     */

    // https://kotlinlang.org/docs/tutorials/android-plugin.html

    // Does not work with com.android.tools.build:gradle:3.2.0-alpha06

    androidExtensions {
        experimental = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
        debug.java.srcDirs += 'src/debug/kotlin'
        release.java.srcDirs += 'src/release/kotlin'
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            crunchPngs true
        }
    }

    packagingOptions {
        exclude 'META-INF/rxjava.properties'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

kapt {
    // https://blog.jetbrains.com/kotlin/2018/01/kotlin-1-2-20-is-out/
    useBuildCache = true
    // https://blog.jetbrains.com/kotlin/2018/03/kotlin-1-2-30-is-out/
    mapDiagnosticLocations = true
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    // Support Library
    implementation 'com.android.support:appcompat-v7:27.1.0'
    implementation 'com.android.support:design:27.1.0'
    implementation 'com.android.support:cardview-v7:27.1.0'
    implementation 'com.android.support:recyclerview-v7:27.1.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'

    // Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'io.reactivex.rxjava2:rxkotlin:2.1.0'
    implementation 'com.github.salomonbrys.kodein:kodein:4.1.0'
    implementation 'com.github.salomonbrys.kodein:kodein-android:4.1.0'
    implementation 'com.github.salomonbrys.kodein:kodein-conf:4.1.0'
    implementation 'androidx.core:core-ktx:0.2'

    // Architecture
    kapt 'android.arch.persistence.room:compiler:1.0.0'
    implementation 'android.arch.persistence.room:rxjava2:1.0.0'
    implementation 'android.arch.lifecycle:extensions:1.1.1'

    // Play, Firebase
    implementation 'com.google.android.gms:play-services-maps:12.0.0'
    implementation 'com.google.android.gms:play-services-location:12.0.0'
    implementation 'com.google.android.gms:play-services-auth:12.0.0'
    implementation 'com.google.firebase:firebase-core:12.0.0'
    implementation 'com.google.firebase:firebase-auth:12.0.0'
    implementation 'com.google.firebase:firebase-database:12.0.0'
    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.1'

    // Commons
    implementation 'commons-io:commons-io:2.5'

    // Testing
    androidTestImplementation 'org.mockito:mockito-android:2.6.2'
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testImplementation 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'
repositories {
    mavenCentral()
}
